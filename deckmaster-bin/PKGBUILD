# Maintainer: Christian Muehlhaeuser <muesli at gmail dot com>

pkgname=deckmaster-bin
pkgver=0.9.0
pkgrel=1
pkgdesc="An application to control your Elgato Stream Deck"
arch=('x86_64')
url="https://github.com/muesli/${pkgname%-bin}"
license=('MIT')
depends=('ttf-roboto')
provides=("${pkgname%-bin}")
conflicts=("${pkgname%-bin}")
source_x86_64=(
    "$url/releases/download/v$pkgver/${pkgname%-bin}_${pkgver}_linux_x86_64.tar.gz"
    '99-deckmaster.rules'
    'deckmaster.path'
    'deckmaster.service')
sha256sums_x86_64=(
    '18d2f6f445638ae2e027738a3a4c06f5c9e9c5d1d878c8b1ed58a5c2172b7738'
    '66d81a5274e1145eb4164aee79d1bffd1dcbe1e0f6608d3de23b32c392827c65'
    '7ba3b3f8c7da2872ff28d19a8f67b4c88da1929213e8379e674bed8a5b72cf18'
    'd2c1338e2a4fb2152f0041ba34995b480fea447f0bd530c260087220a3a870fe')

package() {
    install -Dm755 "${pkgname%-bin}" "$pkgdir/usr/bin/${pkgname%-bin}"
    install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/${pkgname%-bin}/LICENSE"
    install -Dm644 "$srcdir/99-deckmaster.rules" "${pkgdir}/usr/lib/udev/rules.d/99-deckmaster.rules"
    install -Dm644 "$srcdir/deckmaster.path" "${pkgdir}/usr/lib/systemd/user/deckmaster.path"
    install -Dm644 "$srcdir/deckmaster.service" "${pkgdir}/usr/lib/systemd/user/deckmaster.service"
}

# vim:set ts=4 sw=4 et:
